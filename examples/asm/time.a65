;		**** GREENWICH TIME SIGNAL ****
;
; Plays the BBC radio hourly Greenwich time signal pips.
;
; Note that in addition to the PAL-1 Digital Complex Sound Generator card,
; this example requires the RIOT expansion module.
;

sndio 		= $16ee			; SN76489 i/o address
clk1kt		= $1707			; RIOT timer @ 1024T increments

		; Set attenuation
setattn		.macro c, w		; c=channel (0-3), w=weight (0-15)
		lda #(((\c<<1)+9)<<4)+\w
		jsr sendbyte
		.endm

		; Set frequency 
setfreq		.macro c, f		; c=channel (0-3), f=freq (0-1023) 
		lda #(((\c<<1)+9)<<4)+(\f&15)
		jsr sendbyte
		lda #(\f>>4)
		jsr sendbyte
		.endm

		; Delay
delayms		.macro n		; n=delay in ms		
		lda >(\n)
		sta duration
		lda <(\n)
		jsr delay
		.endm

		* = $60

duration	.byte	0		; delay duration

		* = $200

		.for c:=0, c<4, c+=1	; silence tones 1-3 and noise
		setattn c, 15		
		.next

		setfreq	0, 63		; set tone 1 to 1KHz

		ldx #5			; play 5 pips
pip		setattn 0, 5		; set tone 1 attenuation to 10dB
		delayms #100		; play pip for 100ms
		setattn 0, 15		; silence tone 1
		delayms #900		; wait 900ms
		dex
		bne pip			; done playing pips?

		setattn 0, 5		; play final pip for 500ms
		delayms #500		; wait 500ms
		setattn 0, 15		; silence tone 1

		brk			; and done!
		
sendbyte	sta sndio		; (2T + 6T) + 3T send byte to SN76489
		rts			; 6T (17us in total incl. lda/jsr)

delay		sta clk1kt		; wait A milliseconds
delayloop	bit clk1kt
		bpl delayloop		; RIOT timer not done
delaynext	dec duration		; ms wait remaining?
		bmi senddone		; no, all done
		lda #255		; yes, wait another 255ms
		bne delay		; and loop again
senddone	rts

		.end